\newpage
\section{Verification}

As discussed in the introduction, an important feature of Whiley is
{\em verification}.  That is made up of two aspects: firstly, the
ability to write specifications for functions and methods in Whiley;
secondly, the ability of the compiler to check the body of a function
or method meets its specification.

Unfortunately, specification is not always straightforward and
can require considerable attention to detail.  Nevertheless, with
practice, it can easily fit into the routine of day-to-day
development.  In this section, we'll explore the basics of
verification in Whiley using some small examples.  In the following
sections, we'll look at larger and more realistic examples.

\subsection{Preconditions and Postconditions}
A {\em precondition} is a condition over the parameters of a function
that is required to be true when the function is called.  The body of
the function can then use this to make assumptions about the possible
values of the parameters.  Likewise, a {\em postcondition} is a
condition over the return values of a function which is required to be
true after the function is called.  As a very simple example, consider
the following function which accepts a positive integer and returns a
non-negative integer (i.e. natural number):
\begin{lstlisting}
function decrement(int x) => (int y) 
// Parameter x must be greater than zero
requires x > 0
// Return must be greater or equal to zero
ensures y >= 0:
    //
    return x - 1
\end{lstlisting}
Here, the \lstinline{requires} and \lstinline{ensures} clauses define
the function's precondition and postcondition.  With verification
enabled, the Whiley compiler will verify that the
implementation of this function meets its specification.  In fact, we
can see this for ourselves by manually constructing an appropriate
{\em verification condition} (that is, a logical condition whose truth
establishes that the implementation meets its specification).  In this
case, the appropriate verification condition is
 \lstinline{x > 0 ==> x-1 >= 0}.  Unfortunately, although constructing a verification
condition by hand was possible in this case, in general it's difficult
if not impossible for more complex functions.

The Whiley compiler reasons about functions by exploring the different
control-flow paths through their bodies.  Furthermore, as it learns
more about the variables used in the function, it automatically takes
this into account.  For example:
\begin{lstlisting}
function abs(int x) => (int y)
// Return value cannot be negative
ensures y >= 0:
    //
    if x >= 0:
        return x
    else:
        return -x
\end{lstlisting}
The Whiley compiler verifies that the implementation of this function
meets its specification.  At this point, it is worth considering in
more detail what this really means.  Since the Whiley compiler
performs verification at {\em compile-time}, it does not consider
specific values when reasoning about a function's implementation.
Instead, it considers all possible input values for the function which
satisfy its precondition.  In other words, when the Whiley compiler
verifies a function's implementation meets its specification, this
means it does so {\em for all possible input values}.  

\subsection{Data Type Invariants}

The above illustrates a function specification given through explicit pre- and
post-conditions.  However, we may also employ {\em constrained types}
to simplify it as follows:

\begin{lstlisting}
type nat is (int n) where n >= 0
type pos is (int p) where p > 0

function f(pos x) => (nat n)
// Return must differ from parameter
ensures n != x:
    //
    return x-1
\end{lstlisting}

Here, the \lstinline{type} declaration includes a \lstinline{where}
clause constraining the permissible values for the type ($\$$
represents the variable whose type this will be).  Thus,
\lstinline{nat} defines the type of non-negative integers (i.e. the
natural numbers).  Likewise, \lstinline{pos} gives the type of
positive integers and is implicitly a subtype of \lstinline{nat}
(since the constraint on \lstinline{pos} implies that of
\lstinline{nat}).  We consider that good use of constrained types is
critical to ensuring that function specifications remain as readable
as possible.

The notion of type in Whiley is more fluid than found in typical
languages.  In particular, if two types ${\tt T_1}$ and ${\tt T_2}$
have the same {\em underlying} type, then ${\tt T_1}$ is a subtype of
${\tt T_2}$ iff the constraint on ${\tt T_1}$ implies that of ${\tt
  T_2}$.  Consider the following:

\begin{lstlisting}
 type anat is (int x) where x >= 0
 type bnat is (int x) where 2*x >= x

 function f(anat x) => bnat:
     return x
\end{lstlisting}
In this case, we have two alternate (and completely equivalent)
definitions for a natural number (we can see that \lstinline{bnat} is
equivalent to \lstinline{anat} by subtracting \lstinline{x} from both sides).
The Whiley compiler is able to reason that these types are equivalent
and statically verifies that this function is correct.

\subsection{Quantification}
The pre/post-conditions and invariants we have seen above were for constraining primitive types.  But, what if we want to say constrain all elements in collection?  In that case, we need to use a {\em quantifier}, as this allows us to iterate all elements in a collection.  Suppose we wanted to define the type of all integer lists whose elements are non-negative.  We can do this use the {\em universal} \lstinline{all} quantifier in Whiley like so:

\begin{lstlisting}
type ListOfNats is ([int] items)
// every x in items must be greater-or-equal-to zero
where all { x in items | x >= 0 }
\end{lstlisting}

The invariant given in the \lstinline{where} clause simply states that every element in the list must greater-or-equal-to zero.  The \lstinline{all} quantifier is normally read as ``for all''.

In Whiley, there are a range of different quantifiers one can use.  For example, the above could be rewritten using the \lstinline{no} quantifier, like so:

\begin{lstlisting}
type ListOfNats is ([int] items)
// no x in items is less-than zero
where no { x in items | x < 0 }
\end{lstlisting}

Similarly, we could rewrite the above using an {\em existential} quantifier.  Unlike a universal quantifier (which applies to all elements) an existential quantifier applies to just one element.  We can rewrite our \lstinline{ListOfNats} data type using the \lstinline{some} quantifier in Whiley, like so:

\begin{lstlisting}
type ListOfNats is ([int] items)
// does not exist an x in items where is less-than zero
where !some { x in items | x < 0 }
\end{lstlisting}

The \lstinline{some} requires that there is one (or more) elements which meet the criteria, and is normally read as ``there exists''.  Thus, a literal reading of the above invariant would be: {\em it is not the case that there exists an element \lstinline{x} in items where \lstinline{x < 0}}.  Essentially, however, this has the same effect as for the \lstinline{no} quantifier.  

\subsection{Loop Invariants}
\label{loop_invariants}

A loop invariant is a property which
holds before and after each iteration of the loop.  There are three
key points about loop invariants:
\begin{enumerate}
\item The loop invariant must hold on entry to the loop.
\item Assuming the loop invariant holds at the start of the loop body
  (along with the condition), it must hold at the end.
\item The loop invariant (along with the negated condition) can be
  assumed to hold immediately after the loop.
\end{enumerate}

To illustrate these three aspects, we'll use some simple loop
examples.  For example, consider the following example:

\begin{lstlisting}
function f(int x) => (int y)
// return cannot be negative
ensures y >= 0:
    //
    int i = 0
    while i < x where i > 0:
        i = i + 1 
    //
    return i
\end{lstlisting}

Loop invariants in Whiley are indicated by the \lstinline{where}
clause.  Thus, in the above example, the loop invariant is
``\lstinline{i > 0}''.  Compiling the above program with verification
enabled will fail with an error.   This is because the loop invariant
does not hold on entry to the loop (item 1 above).

\subsection{Strategies for Loop Invariants}
Loop invariants can be tricky to get right, and there are some useful
tricks which can simplify things.  We'll now consider some examples to
illustrate this.

\paragraph{Example 1.} Summing over a list of natural numbers is
guaranteed to produce a natural number.  The following Whiley program
illustrates this:

\begin{lstlisting}
type nat is (int x) where x >= 0

function sum([nat] items) => nat:
    int r = 0
    int i = 0
    //
    while i < |items| where i >= 0 && r >= 0:
        r = r + items[i]
        i = i + 1
    //
    return r
\end{lstlisting}
The Whiley compiler statically verifies that \lstinline{sum()} does
indeed meet this specification.  This is true in Whiley because
integer arithmetic is {\em unbounded} --- meaning it does not suffer
from overflow as other languages do (e.g. Java).  The loop invariant
is necessary to help the Whiley compiler verify this function.
However, we can avoid the need for a loop invariant by declaring
variables \lstinline{i} and \lstinline{r} more precisely:
\begin{lstlisting}
function sum([nat] items) => nat:
    nat r = 0
    nat i = 0
    //
    while i < |items|:
        ...
\end{lstlisting}

This time, we have declared the variables \lstinline{i} and
\lstinline{r} as having type \lstinline{nat}.  The Whiley compiler
will now enforce the \lstinline{nat} property for \lstinline{i} and
\lstinline{r} at all points in the function, and the loop invariant is
no longer required.

\paragraph{Example 2.}  Generally speaking, the loop condition and
invariant are used independently to increase knowledge.  However,
sometimes they need to be used in concert.  Consider the following
function for initialising a list of a given size:

\begin{lstlisting}
function create(int count, int value) => ([int] r)
// Cannot create negatively sized lists!
requires count >= 0, 
// Returned list must have count elements
ensures |r| == count:
   //
   int i = 0
   [int] r = []
   while i < count:
      r = r + [value]
      i = i + 1
   //
   return r
\end{lstlisting}

This example uses the list append operator (i.e. \lstinline{r + [value]})
 and is surprisingly challenging to verify.  An obvious
approach is to connect the size of \lstinline{r} with \lstinline{i} as
follows:

\begin{lstlisting}
   ...
   while i < count where |r| == i:
      ...
\end{lstlisting}

Unfortunately, this loop invariant is not strong enough to allow this
function to be verified.  To understand this, recall from
\S\ref{loop_invariants} that, after a loop is complete, the loop
invariant holds along with the {\em negated} condition.  Thus, after
the above loop, we have \lstinline{i >= count && |r| == i} which is
insufficient to establish \lstinline{|r| == count}.  In fact, we can
resolve this by using an {\em overriding loop invariant} as follows:

\begin{lstlisting}
   ...
   while i < count where i <= count && |r| == i:
      ...
\end{lstlisting}

In this case, \lstinline{i >= count && i <= count && |r| == i} holds
after the loop and, hence, it follows that \lstinline{|r| == count}.

\subsection{Explicit Assumptions}

\subsection{Function Invocation}

To keep verification tractable, the Whiley compiler verifies each
function in a program one at a time, independently of
others.\footnote{This corresponds to performing an {\em
    intra-procedural} analysis, compared with a more involved {\em
    inter-procedural} analysis.}  Thus, when verifying a given
function, it assumes that all other functions correctly meet their
specification.  Of course, if this is not the case, then this will
eventually be discovered as the compiler progresses through the
program.  For example, consider this program:

\begin{lstlisting}
function f(int x) => (int y)
// Return cannot be negative
ensures y >= 0:
    //
    return x

function g() => (int y)
// Return cannot be negative
ensures y >= 0:
    //
    return f(1)
\end{lstlisting}

This program will not verify because the implementation of
\lstinline{f()} does not meet its specification.  For example,
\lstinline{f(-1)} gives \lstinline{-1} but the post-condition for
\lstinline{f()} allows only non-negative integers to be returned.
However, the Whiley compiler will verify that the implementation of
\lstinline{g()} meets its specification as, when doing this, it
assumes that \lstinline{f()} meets it specification.
