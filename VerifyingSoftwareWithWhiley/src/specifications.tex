\chapter{Specifying Programs}
In this chapter, we embark upon the first step in the process of establishing that a program is (in some sense) correct.  That is, to determine exactly what our program is supposed to do.  To this end, we will learn how to write {\em specifications} for our programs which precisely describe their behaviour.  


Specifying a program in Whiley consists of at least two separate activities;  firstly, we provide appropriate specifications (called
{\em invariants}) for any data types we have defined;  secondly, we provide specifications in the form of {\em pre-} and {\em post-conditions} for any functions or methods defined.  In doing this, we must acknowledge that precisely describing a program's behaviour is extremely challenging and, oftentimes, we want only to specify some {\em important aspect} of its permitted behaviour.  This can give us many of the benefits from specification without all of the costs.  

Having specified our program, we want to establish that it meets its specification.  Or, in other words, that every function and method meets its specification.  This is itself quite a challenge and, furthermore, will often uncover critical flaws in our program.  While we will consider how to write specifications in this chapter, we will largely ignore the issue of determining whether our programs meet their specifications.  Although this is an important part of the process, it is also challenging and we will require several chapters to fully explore how this is done.

{\em Clarify that there are different levels of specification, ranging from the minimum to illustrate lack of runtime error to the complete specification that a function is correct.}

\section{Specifications as Contracts}
{\em Illustrate the basic idea of what a contract is using English language}.
A specification is a contract between two parties: the {\em client} and {\em supplier}.  The client represents the person(s) using the given program, whilst the supplier is the person(s) implementing the program.  The specification ties the {\em inputs} of the program to its {\em outputs} in two ways:
\begin{itemize}
\item {\bf Inputs.} The specification states what is {\em required} of the inputs for the program to behave correctly.  The client is responsible for ensuring the correct inputs are always given.  If an incorrect input is given, the contract is broken and the program may do something unexpected.
\item {\bf Outputs.} The specification states what outputs must be {\em ensured} by a correctly behaving program.  The supplier is responsible for ensuring all outputs meet the specification, assuming that correct inputs were provided.
\end{itemize}
From this, we can see that both parties in the contract have obligations they must meet.  This also allows us to think about {\em blame}.  That is when something goes wrong {\em who is responsible?}  If the inputs to our program we incorrect according to the specification, we can blame the client.  On the other hand if the inputs were correct, but the outputs were not, then we can blame the supplier.

\begin{eg}
As a simple example, let us consider a function for finding the maximum values from an array of integer values.  The following provides an {\em informal} specification for this function:
\begin{lstlisting}
// REQUIRES: At least one item in items array
//
// ENSURES: Item returned was largest item in items array
function max([int] items) -> (int item)
\end{lstlisting}
We have specified our function above using comments to document: firstly, the requirements needed for the inputs (i.e. that the array must have at least one element); and, secondly, the expectations placed on the outputs (i.e. that it returns the largest element in the array).  Thus, we could not expect the call \lstinline{max([])} to operate correctly; likewise, if the call \lstinline{max([1,2])} returned \lstinline{3} we would rightly say the implementation was incorrect.
\end{eg}

{\em Could talk about limitations of english language comments}

\section{Specifying Functions}
To specify a function or method in Whiley we must provide an appropriate {\em precondition} and {\em postcondition}.  A precondition is a condition over the parameters of a function that is required to be true when the function is called.  The body of the function can then use this to make assumptions about the possible values of the parameters.  Likewise, a postcondition is a condition over the return values of a function which is required to be true after the function is called.  

\begin{eg}
As a very simple example, consider the following specification for our function which finds the maximum value from an array integers:
\begin{lstlisting}
function max([int] items) => (int item) 
// Must have one or more items
requires |items| > 0
// Value returned not smaller than anything in items
ensures all { i in 0 .. |items| | items[i] <= item }
// Value returns was in items array
ensures exists { i in 0 .. |items| | items[i] == item }
\end{lstlisting}
Here, the \lstinline{requires} clause declares the function's precondition, whilst the \lstinline{ensures} clauses declare its postcondition.  This specification is largely the same as that given informally using comments before.  However, we regard this specification as being {\em formal} because, for any set of inputs and outputs, we can calculate precisely whether the inputs or outputs were malformed.  For example, consider the call \lstinline{max([])}.  We can say that the inputs to this call are incorrect, because \lstinline{|[]| < 0} evaluates to \lstinline{false}.  For the informal version given above, we cannot easily evaluate the English comments to determine whether they were met or not.  Instead, we rely on our human judgement for this --- but, unfortunately, this can easily be wrong!
\end{eg}

When specifying a function in Whiley, the \lstinline{requires} clause(s) may only refer to the parameters, whilst the \lstinline{ensures} clause(s) may also refer to the returns.  Note, however, that the \lstinline{ensures} clause(s) always consider those values of the parameters that held on entry to the function, not those which might hold at the end.

\subsection{Meeting the Contract (Supplier)}

We will now consider more precisely what it means for the supplier to meet the contract set out in a function's specification.  {\em For all possible inputs}.

\begin{eg}
To illustrate a function which does not meet its specification, consider the following incorrect implementation of our \lstinline{max()} function:
\begin{lstlisting}
function max([int] items) => (int item) 
// Must have one or more items
requires |items| > 0
// Value returned not smaller than anything in items
ensures all {i in 0 .. |items| | items[i] <= item}
// Value returns was in items array
ensures exists {i in 0 .. |items| | items[i] == item}:
   //
   return items[0]
\end{lstlisting}
By looking at the function, we can see that it does not do what we expect.  But, let's think about this more formally.  If the implementation is to be considered incorrect, {\em there must be at least one valid input which yields an incorrect output}.  So, to show out implementation is incorrect, we must find such an input.  Let us consider the input \lstinline{[5]}, for which our implementation returns \lstinline{5}.  In this case, both \lstinline!all {i in 0..|[5]| | [5][i] <= 5}! and \lstinline!exists{i in 0..|[5]| | [5][i] == 5}! evaluate to \lstinline{true} and, hence, this input does not illustrate the problem.  In contrast, for the input \lstinline{[1,4]}, our implementation returns \lstinline{1}.  In this case, \lstinline!all{ i in 0..|[1,4]| | [1,4][i] <= 1}! evaluates to \lstinline{false}.  Hence, this input provides evidence that our implementation does not meet its contract.
\end{eg}

{\em Talk about challenge in proving a function is correct and how need to use Whiley for this.}

\begin{eg} We can now illustrate a correct implementation of our \lstinline{max()} function as follows:

\begin{lstlisting}
function max([int] items) => (int item) 
// Must have one or more items
requires |items| > 0
// Value returned not smaller than anything in items
ensures all {i in 0 .. |items| | items[i] <= item}
// Value returns was in items array
ensures exists {i in 0 .. |items| | items[i] == item}:
   //
   int i = 0
   int r = items[0]
   //
   while i < |items:
      if r < items[i]:
          r = items[i]
      i = i + 1
   //
   return r
\end{lstlisting}

Understanding that this function does indeed meet is specification is not easy.  Perhaps, intuitively, we can convinced ourselves that it is using a number of arguments.  For example, the variable \lstinline{r} is only ever assigned values from the \lstinline{items} array and, hence, the implementation must meet the second \lstinline{ensures} clause of the specification.  Similarly, since the loop iterates through and compares all elements in \lstinline{items}, we believe the first \lstinline{ensures} clause is met as well.  But, again, {\em we are relying on our error-prone human judgement here}.  
\end{eg}

{\em Briefly outline need for tools to do calculations for us, and how Whiley is one such example}.

\subsection{Meeting the Contract (Client)}

\begin{itemize}

\item Multiple requires / ensures permitted
\item Bodies of called functions ignored
\item Pre/Post-conditions must be true before being called
\end{itemize}

\section{Specifying Data}

The above illustrates a function specification given through explicit pre- and
post-conditions.  However, we may also employ {\em constrained types}
to simplify it as follows:

\begin{lstlisting}
type nat is (int n) where n >= 0
type pos is (int p) where p > 0

function f(pos x) => (nat n)
// Return must differ from parameter
ensures n != x:
    //
    return x-1
\end{lstlisting}

Here, the \lstinline{type} declaration includes a \lstinline{where}
clause constraining the permissible values for the type ($\$$
represents the variable whose type this will be).  Thus,
\lstinline{nat} defines the type of non-negative integers (i.e. the
natural numbers).  Likewise, \lstinline{pos} gives the type of
positive integers and is implicitly a subtype of \lstinline{nat}
(since the constraint on \lstinline{pos} implies that of
\lstinline{nat}).  We consider that good use of constrained types is
critical to ensuring that function specifications remain as readable
as possible.

The notion of type in Whiley is more fluid than found in typical
languages.  In particular, if two types ${\tt T_1}$ and ${\tt T_2}$
have the same {\em underlying} type, then ${\tt T_1}$ is a subtype of
${\tt T_2}$ iff the constraint on ${\tt T_1}$ implies that of ${\tt
  T_2}$.  Consider the following:

\begin{lstlisting}
 type anat is (int x) where x >= 0
 type bnat is (int x) where 2*x >= x

 function f(anat x) => bnat:
     return x
\end{lstlisting}
In this case, we have two alternate (and completely equivalent)
definitions for a natural number (we can see that \lstinline{bnat} is
equivalent to \lstinline{anat} by subtracting \lstinline{x} from both sides).
The Whiley compiler is able to reason that these types are equivalent
and statically verifies that this function is correct.

\section{Weak vs Strong Specifications}
\section{Static vs Dynamic Checking}
\section{Partial vs Total Correctness}

\section{Exercises}

\begin{ex}
The function \lstinline{neg()} returns the arithmetic negation of a value.
For example, \lstinline{neg(1) = -1}.  An implementation of
this function is given as follows:
\begin{lstlisting}
function neg(int x) => (int r):
    return -x
\end{lstlisting}
Provide an appropriate {\em post-condition} for this function.
\end{ex}

\begin{ex}
The \lstinline{swap} function accepts to integers and returns them
with their order swapped.  The signature for the function is:
\begin{lstlisting}
function swap(int x, int y) => (int a, int b):
    ...
\end{lstlisting}
Provide an appropriate specification and implementation for this function.
\end{ex}

\begin{ex}

A {\em natural} number is an integer which is greater-than-or-equal to
zero.  The following function adds three natural numbers together to produce a natural number:
\begin{lstlisting}
function sum3(int x, int y, int z) => (int r)
// No parameter can be negative
requires ...
// Return value cannot be negative
ensures ...:
    //
    return x + y + z
\end{lstlisting}
Complete the given \lstinline{requires} and \lstinline{ensures}
clauses based on the given English descriptions.

\end{ex}

\begin{ex}
  The following function computes the absolute difference between two values:

\begin{lstlisting}
function diff(int x, int y) => (int r):
    //
    if x > y:
        return x - y
    else:
        return y - x
\end{lstlisting}

A {\em pre-condition} of this function is that parameter \lstinline{x}
is between \lstinline{0} and \lstinline{255} (inclusive) and,
likewise, that variable \lstinline{y} is between \lstinline{-128} and
\lstinline{127} (inclusive).  Provide a partial specification for
this function which constraints the ranges of the input and output
variables as tightly as possible.
\end{ex}

\begin{ex}
The Gregorian calendar is the most widely used organisation of dates.
A well-known saying for remembering the number of days in each month
is the following:
\begin{quote}
``Thirty days hath September, April, June and November.  All the rest
have thirty-one, except February which has twenty-nine ...''
\end{quote}
Note, in this exercise, we will ignore the issue of leap years.  A
simple function for returning a date can be defined as follws:
\begin{lstlisting}
constant Jan is 1
constant Feb is 2
constant Mar is 3
constant Apr is 4
constant May is 5
constant Jun is 6
constant Jul is 7
constant Aug is 8
constant Sep is 9
constant Oct is 10
constant Nov is 11
constant Dec is 12

function getDate() => (int day, int month, int year):
    ...
\end{lstlisting}
Provide a specification for this function to ensure the returned date
is valid (ignoring leap years).  Furthermore, provide a simple
implementation which meets this specification.
\end{ex}

\begin{ex}
A well-known puzzle is that of the three water jugs.  In this
exercise, we will consider a cut down version of this which consists
of two water jugs: a small jug (containing three litres) and a large
jug (containing five litres).  The goal is to complete the
specification of the following function for pouring water from the
small jug into the large jug:

\begin{lstlisting}
function pourSmall2Large(int smallJug, int largeJug) => 
                        (int smallJugAfter, int largeJugAfter)
// The small jug holds between 0 and 3 litres (before)
requires ...
// The large jug holds between 0 and 5 litres (before)
requires ...
// The small jug holds between 0 and 3 litres (after)
ensures ...
// The large jug holds between 0 and 5 litres (after)
ensures ...
// The amount in both jugs is unchanged by this function
ensures ...
// Afterwards, either the small jug is empty or the large jug is full
ensures ...:
    //
    if smallJug + largeJug <= 5:
        // indicates we're emptying the small jug
        largeJug = largeJug + smallJug
        smallJug = 0
    else:
        // indicates we're filling up the medium jug    
        smallJug = largeJug + smallJug
        largeJug = 5
    // Done
    return smallJug, largeJug
\end{lstlisting}
Complete the missing \lstinline{requires} and \lstinline{ensures}
clauses based on the given English descriptions.  Does the implementation meet the given specification?
\end{ex}
